#version 150

#define SMALL_TEXTURE_SIZE int(16)

#define SMALL_BLOCK_FALL_IN_DURATION int(250)
#define SMALL_BLOCK_FALL_IN_DELAY int(62)
#define SMALL_IDLE_DURATION int(1500)
#define SMALL_BLOCK_FALL_OUT_DURATION int(125)
#define SMALL_BLOCK_FALL_OUT_DELAY int(10)

#define SMALL_BLOCK_FALL_IN_DELTA_Y int(-25)
#define SMALL_BLOCK_FALL_IN_OPACITY float(0)

#define SMALL_BLOCK_FALL_OUT_DELTA_Y int(10)
#define SMALL_BLOCK_FALL_OUT_OPACITY float(0)

#define SMALL_DELTA_X_PER_X int(-(SMALL_TEXTURE_SIZE / 2 - SMALL_TEXTURE_SIZE / 16 - 1))
#define SMALL_DELTA_X_PER_Y int(0)
#define SMALL_DELTA_X_PER_Z int((SMALL_TEXTURE_SIZE / 2 - SMALL_TEXTURE_SIZE / 16 - 1))

#define SMALL_DELTA_Y_PER_X int(SMALL_TEXTURE_SIZE / 4 - 1)
#define SMALL_DELTA_Y_PER_Y int(-SMALL_TEXTURE_SIZE / 2)
#define SMALL_DELTA_Y_PER_Z int(SMALL_TEXTURE_SIZE / 4 - 1)

#define SMALL_BASE_DELTA_Y int(SMALL_TEXTURE_SIZE / 2)
#define SMALL_DELTA_Y_PER_HEIGHT_UNIT int(SMALL_TEXTURE_SIZE)

const vec4 smallStructureMarkerColor = vec4(21, 44, 193, 2) / 255;
const vec4 finalSmallStructureMarkerColor = vec4(21, 44, 194, 2) / 255;

int smallCalculateBlockBaseDeltaX(int index, int sizeX, int sizeY, int sizeZ) {
    if (index == 0) {
        return -SMALL_TEXTURE_SIZE / 2;
    } else {
        ivec3 position = fromIndex(index, sizeX, sizeY, sizeZ);
        return position.x * SMALL_DELTA_X_PER_X 
                + position.y * SMALL_DELTA_X_PER_Y 
                + position.z * SMALL_DELTA_X_PER_Z
                - (SMALL_TEXTURE_SIZE + 1) * index
                - SMALL_TEXTURE_SIZE / 2;
    }
}

int smallCalculateBlockBaseDeltaY(int index, int sizeX, int sizeY, int sizeZ) {
    ivec3 position = fromIndex(index, sizeX, sizeY, sizeZ);
    return position.x * SMALL_DELTA_Y_PER_X 
                + position.y * SMALL_DELTA_Y_PER_Y 
                + position.z * SMALL_DELTA_Y_PER_Z 
                - SMALL_TEXTURE_SIZE / 2 
                - SMALL_TEXTURE_SIZE * sizeY / 2
                + max(sizeX, sizeZ) * SMALL_TEXTURE_SIZE / 2;
}

void processStructureSmall() {
    vec4 color = texture(Sampler0, UV);
    bool isFinal = finalSmallStructureMarkerColor == color;
    if (smallStructureMarkerColor != color && !isFinal) {
        return;
    }
    int r = int(round(newColor.r * 255));
    int g = int(round(newColor.g * 255));
    int b = int(round(newColor.b * 255));
    int blockX = (r >> 4) % 16;
    int sizeX = r % 16;
    int blockY = (g >> 4) % 16;
    int sizeY = g % 16;
    int renderSizeY;
    bool isOneLayer = blockY >= sizeY;
    if (isOneLayer) {
        blockY -= sizeY;
        renderSizeY = 1;
    } else {
        renderSizeY = sizeY;
    }
    int blockZ = (b >> 4) % 16;
    int sizeZ = b % 16;

    newColor.r = 1;
    newColor.g = 1;
    newColor.b = 1;
    if (sizeX == 0 || sizeY == 0 || sizeZ == 0) {
        return;
    }
    
    if (isFinal) {
        UV /= 2;
    }
    
    int blockIndex = toIndex(blockX, blockY, blockZ, sizeX, renderSizeY, sizeZ);
    int blocksCount = sizeX * renderSizeY * sizeZ;
    
    int fallInDurationMillis = SMALL_BLOCK_FALL_IN_DELAY * (blocksCount - 1) + SMALL_BLOCK_FALL_IN_DURATION;
    int fallOutDurationMillis = SMALL_BLOCK_FALL_OUT_DELAY * (blocksCount - 1) + SMALL_BLOCK_FALL_OUT_DURATION;
    int durationMillis = fallInDurationMillis + SMALL_IDLE_DURATION + fallOutDurationMillis;

    int currentAnimationMillis = int(round(GameTime * 24000 * 50)) % durationMillis;

    int deltaX = smallCalculateBlockBaseDeltaX(blockIndex, sizeX, sizeY, sizeZ); 
    int deltaY = smallCalculateBlockBaseDeltaY(blockIndex, sizeX, sizeY, sizeZ);

    ivec3 position = fromIndex(blockIndex, sizeX, sizeY, sizeZ);

    newPosition.x += deltaX;
    newPosition.z += blockIndex;
    if (currentAnimationMillis <= fallInDurationMillis) {
        int startAnimationMillis = SMALL_BLOCK_FALL_IN_DELAY * blockIndex;
        float stage = 0;
        if (currentAnimationMillis >= startAnimationMillis) {
            if (currentAnimationMillis >= startAnimationMillis + SMALL_BLOCK_FALL_IN_DURATION) {
                stage = 1;
            } else {
                stage = float(currentAnimationMillis - startAnimationMillis) / float(SMALL_BLOCK_FALL_IN_DURATION);
            }
        }


        float positionStage = easeInOutCubic(stage);
        float colorStage = easeInOutCubic(stage);

        newPosition.y += deltaY + int(round((1 - positionStage) * SMALL_BLOCK_FALL_IN_DELTA_Y));
        newColor.a = colorStage;
    } else if (currentAnimationMillis <= fallInDurationMillis + SMALL_IDLE_DURATION) {
        newPosition.y += deltaY;

        if (isFinal) {
            UV.y += 0.5;
        }
    } else {
        int startAnimationMillis = SMALL_BLOCK_FALL_OUT_DELAY * blockIndex;
        float stage = 0;
        int localAnimationMillis = currentAnimationMillis - fallInDurationMillis - SMALL_IDLE_DURATION;
        if (localAnimationMillis >= startAnimationMillis) {
            if (localAnimationMillis >= startAnimationMillis + SMALL_BLOCK_FALL_OUT_DURATION) {
                stage = 1;
            } else {
                stage = float(localAnimationMillis - startAnimationMillis) / float(SMALL_BLOCK_FALL_OUT_DURATION);
            }
        }


        float positionStage = easeInOutCubic(stage);
        float colorStage = easeInOutCubic(stage);

        newPosition.y += deltaY + int(round(positionStage * SMALL_BLOCK_FALL_OUT_DELTA_Y));
        newColor.a = (1 - colorStage);
    }
    
}